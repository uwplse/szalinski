use <ttf/PathwayGothicOne-Regular.ttf>
//Textline 1
textstring1="Producer - Material";

//Textline 2 (Left bottom line), e.g. for layer thickness, print temperature
textstring2=" 0.2  235Â°";

//Textline 3 (Right bottom line)
textstring3="color";

//Top text (on the top edge, to quickly find the material type). 
texttop=" P E T G";//[,P L A, A B S, P E T G, N y l o n, H I P S, P E E K, T P U, P O M, P V A, P P, A S A, P C, U L T E M, R e s i n]

//Font name, e.g. "Pathway Gothic One,", "Voltaire", "Oswald", depends on what is available on the platform. In the customizer,choose from about 700 Google Fonts. For an overview please refer: http://www.google.com/fonts. If you run OpenSCAD on your own PC, use **Aldo** as font.
fontname="Pathway Gothic One";//[Pathway Gothic One,Voltaire,Oswald,Oswald:style=Bold,PT Sans Narrow:style=Bold,Archivo,ABeeZee,Abel,Abril Fatface,Aclonica,Acme,Actor,Adamina,Advent Pro,Aguafina Script,Akronim,Aladin,Aldrich,Alef,Alegreya,Alegreya Sans,Alegreya Sans SC,Alegreya SC,Alex Brush,Alfa Slab One,Alice,Alike,Alike Angular,Allan,Allerta,Allerta Stencil,Allura,Almendra,Almendra Display,Almendra SC,Amarante,Amaranth,Amatic SC,Amethysta,Amiri,Anaheim,Andada,Andika,Angkor,Annie Use Your Telescope,Anonymous Pro,Antic,Antic Didone,Antic Slab,Anton,Arapey,Arbutus,Arbutus Slab,Architects Daughter,Archivo Black,Archivo Narrow,Arimo,Arizonia,Armata,Artifika,Arvo,Asap,Asset,Astloch,Asul,Atomic Age,Aubrey,Audiowide,Autour One,Average,Average Sans,Averia Gruesa Libre,Averia Libre,Averia Sans Libre,Averia Serif Libre,Bad Script,Balthazar,Bangers,Basic,Battambang,Baumans,Bayon,Belgrano,Belleza,BenchNine,Bentham,Berkshire Swash,Bevan,Bigelow Rules,Bigshot One,Bilbo,Bilbo Swash Caps,Bitter,Black Ops One,Bokor,Bonbon,Boogaloo,Bowlby One,Bowlby One SC,Brawler,Bree Serif,Bubblegum Sans,Bubbler One,Buda,Buenard,Butcherman,Butterfly Kids,Cabin,Cabin Condensed,Cabin Sketch,Caesar Dressing,Cagliostro,Calligraffitti,Cambay,Cambo,Candal,Cantarell,Cantata One,Cantora One,Capriola,Cardo,Carme,Carrois Gothic,Carrois Gothic SC,Carter One,Caudex,Cedarville Cursive,Ceviche One,Changa One,Chango,Chau Philomene One,Chela One,Chelsea Market,Chenla,Cherry Cream Soda,Cherry Swash,Chewy,Chicle,Chivo,Cinzel,Cinzel Decorative,Clicker Script,Coda,Coda Caption,Codystar,Combo,Comfortaa,Coming Soon,Concert One,Condiment,Content,Contrail One,Convergence,Cookie,Copse,Corben,Courgette,Cousine,Coustard,Covered By Your Grace,Crafty Girls,Creepster,Crete Round,Crimson Text,Croissant One,Crushed,Cuprum,Cutive,Cutive Mono,Damion,Dancing Script,Dangrek,Dawning of a New Day,Days One,Dekko,Delius,Delius Swash Caps,Delius Unicase,Della Respira,Denk One,Devonshire,Dhurjati,Didact Gothic,Diplomata,Diplomata SC,Domine,Donegal One,Doppio One,Dorsa,Dosis,Dr Sugiyama,Droid Sans,Droid Sans Mono,Droid Serif,Duru Sans,Dynalight,Eagle Lake,Eater,EB Garamond,Economica,Ek Mukta,Electrolize,Elsie,Elsie Swash Caps,Emblema One,Emilys Candy,Engagement,Englebert,Enriqueta,Erica One,Esteban,Euphoria Script,Ewert,Exo,Exo 2,Expletus Sans,Fanwood Text,Fascinate,Fascinate Inline,Faster One,Fasthand,Fauna One,Federant,Federo,Felipa,Fenix,Finger Paint,Fira Mono,Fira Sans,Fjalla One,Fjord One,Flamenco,Flavors,Fondamento,Fontdiner Swanky,Forum,Francois One,Freckle Face,Fredericka the Great,Fredoka One,Freehand,Fresca,Frijole,Fruktur,Fugaz One,Gabriela,Gafata,Galdeano,Galindo,Gentium Basic,Gentium Book Basic,Geo,Geostar,Geostar Fill,Germania One,GFS Didot,GFS Neohellenic,Gidugu,Gilda Display,Give You Glory,Glass Antiqua,Glegoo,Gloria Hallelujah,Goblin One,Gochi Hand,Gorditas,Goudy Bookletter 1911,Graduate,Grand Hotel,Gravitas One,Great Vibes,Griffy,Gruppo,Gudea,Gurajada,Habibi,Halant,Hammersmith One,Hanalei,Hanalei Fill,Handlee,Hanuman,Happy Monkey,Headland One,Henny Penny,Herr Von Muellerhoff,Hind,Holtwood One SC,Homemade Apple,Homenaje,Iceberg,Iceland,IM Fell Double Pica,IM Fell Double Pica SC,IM Fell DW Pica,IM Fell DW Pica SC,IM Fell English,IM Fell English SC,IM Fell French Canon,IM Fell French Canon SC,IM Fell Great Primer,IM Fell Great Primer SC,Imprima,Inconsolata,Inder,Indie Flower,Inika,Irish Grover,Istok Web,Italiana,Italianno,Jacques Francois,Jacques Francois Shadow,Jim Nightshade,Jockey One,Jolly Lodger,Josefin Sans,Josefin Slab,Joti One,Judson,Julee,Julius Sans One,Junge,Jura,Just Another Hand,Just Me Again Down Here,Kalam,Kameron,Kantumruy,Karla,Karma,Kaushan Script,Kavoon,Kdam Thmor,Keania One,Kelly Slab,Kenia,Khand,Khmer,Khula,Kite One,Knewave,Kotta One,Koulen,Kranky,Kreon,Kristi,Krona One,La Belle Aurore,Laila,Lakki Reddy,Lancelot,Lateef,Lato,League Script,Leckerli One,Ledger,Lekton,Lemon,Liberation Sans,Libre Baskerville,Life Savers,Lilita One,Lily Script One,Limelight,Linden Hill,Lobster,Lobster Two,Londrina Outline,Londrina Shadow,Londrina Sketch,Londrina Solid,Lora,Love Ya Like A Sister,Loved by the King,Lovers Quarrel,Luckiest Guy,Lusitana,Lustria,Macondo,Macondo Swash Caps,Magra,Maiden Orange,Mako,Mallanna,Mandali,Marcellus,Marcellus SC,Marck Script,Margarine,Marko One,Marmelad,Martel Sans,Marvel,Mate,Mate SC,Maven Pro,McLaren,Meddon,MedievalSharp,Medula One,Megrim,Meie Script,Merienda,Merienda One,Merriweather,Merriweather Sans,Metal,Metal Mania,Metamorphous,Metrophobic,Michroma,Milonga,Miltonian,Miltonian Tattoo,Miniver,Miss Fajardose,Modak,Modern Antiqua,Molengo,Molle,Monda,Monofett,Monoton,Monsieur La Doulaise,Montaga,Montez,Montserrat,Montserrat Alternates,Montserrat Subrayada,Moul,Moulpali,Mountains of Christmas,Mouse Memoirs,Mr Bedfort,Mr Dafoe,Mr De Haviland,Mrs Saint Delafield,Mrs Sheppards,Muli,Mystery Quest,Neucha,Neuton,New Rocker,News Cycle,Niconne,Nixie One,Nobile,Nokora,Norican,Nosifer,Nothing You Could Do,Noticia Text,Noto Sans,Noto Serif,Nova Cut,Nova Flat,Nova Mono,Nova Oval,Nova Round,Nova Script,Nova Slim,Nova Square,NTR,Numans,Nunito,Odor Mean Chey,Offside,Old Standard TT,Oldenburg,Oleo Script,Oleo Script Swash Caps,Open Sans,Open Sans Condensed,Oranienbaum,Orbitron,Oregano,Orienta,Original Surfer,Over the Rainbow,Overlock,Overlock SC,Ovo,Oxygen,Oxygen Mono,Pacifico,Paprika,Parisienne,Passero One,Passion One,Patrick Hand,Patrick Hand SC,Patua One,Paytone One,Peddana,Peralta,Permanent Marker,Petit Formal Script,Petrona,Philosopher,Piedra,Pinyon Script,Pirata One,Plaster,Play,Playball,Playfair Display,Playfair Display SC,Podkova,Poiret One,Poller One,Poly,Pompiere,Pontano Sans,Port Lligat Sans,Port Lligat Slab,Prata,Preahvihear,Press Start 2P,Princess Sofia,Prociono,Prosto One,PT Mono,PT Sans,PT Sans Caption,PT Sans Narrow,PT Serif,PT Serif Caption,Puritan,Purple Purse,Quando,Quantico,Quattrocento,Quattrocento Sans,Questrial,Quicksand,Quintessential,Qwigley,Racing Sans One,Radley,Rajdhani,Raleway,Raleway Dots,Ramabhadra,Ramaraja,Rambla,Rammetto One,Ranchers,Rancho,Ranga,Rationale,Ravi Prakash,Redressed,Reenie Beanie,Revalia,Ribeye,Ribeye Marrow,Righteous,Risque,Roboto,Roboto Condensed,Roboto Slab,Rochester,Rock Salt,Rokkitt,Romanesco,Ropa Sans,Rosario,Rosarivo,Rouge Script,Rozha One,Rubik Mono One,Rubik One,Ruda,Rufina,Ruge Boogie,Ruluko,Rum Raisin,Ruslan Display,Russo One,Ruthie,Rye,Sacramento,Sail,Salsa,Sanchez,Sancreek,Sansita One,Sarina,Sarpanch,Satisfy,Scada,Scheherazade,Schoolbell,Seaweed Script,Sevillana,Seymour One,Shadows Into Light,Shadows Into Light Two,Shanti,Share,Share Tech,Share Tech Mono,Shojumaru,Short Stack,Siemreap,Sigmar One,Signika,Signika Negative,Simonetta,Sintony,Sirin Stencil,Six Caps,Skranji,Slabo 13px,Slabo 27px,Slackey,Smokum,Smythe,Sniglet,Snippet,Snowburst One,Sofadi One,Sofia,Sonsie One,Sorts Mill Goudy,Source Code Pro,Source Sans Pro,Source Serif Pro,Special Elite,Spicy Rice,Spinnaker,Spirax,Squada One,Sree Krushnadevaraya,Stalemate,Stalinist One,Stardos Stencil,Stint Ultra Condensed,Stint Ultra Expanded,Stoke,Strait,Sue Ellen Francisco,Sunshiney,Supermercado One,Suranna,Suravaram,Suwannaphum,Swanky and Moo Moo,Syncopate,Tangerine,Taprom,Tauri,Teko,Telex,Tenali Ramakrishna,Tenor Sans,Text Me One,The Girl Next Door,Tienne,Timmana,Tinos,Titan One,Titillium Web,Trade Winds,Trocchi,Trochut,Trykker,Tulpen One,Ubuntu,Ubuntu Condensed,Ubuntu Mono,Ultra,Uncial Antiqua,Underdog,Unica One,UnifrakturCook,UnifrakturMaguntia,Unkempt,Unlock,Unna,Vampiro One,Varela,Varela Round,Vast Shadow,Vesper Libre,Vibur,Vidaloka,Viga,Voces,Volkhov,Vollkorn,VT323,Waiting for the Sunrise,Wallpoet,Walter Turncoat,Warnes,Wellfleet,Wendy One,Wire One,Yanone Kaffeesatz,Yellowtail,Yeseva One,Yesteryear,Zeyada]


//Set font size in mm
textsize=5; 

//Text raised or embossed
text_type="embossed";//[embossed,raised]

//Advanced test swatch patterns active?
test_patterns="enabled";//[enabled,disabled]

//Number of advanced test circles?
test_circles=5;//[0..5]

// Thickness progression of the 'layers' is taken from https://www.thingiverse.com/thing:3067940 : 0.2, 0.4, 0.6, 0.8, 1.0, 1.5, without the 2.25 mm. If you put a negative number here, it will generate a "bridge" structure of the respective thickness.
thickness_steps=[0.2, 0.4, 0.6, 0.8, 1.0, 2.0]; //no 2.3, as thats the swatch thickness.

//textheight on thickness steps
textheight_on_steps=.4;

//Swatch size. This is compatible to Jaxels box. Other sizes are e.g. 75,40,3. 
//Swatch Width
w=80.0; 
//Swatch Height
h=35; 
//Swatch Thickness
th=3.0;
 
//Swatch roundness radius
round=5;

//Border size around the text
wall=2.5;

//Text line separation
linesep=1.4;

//hole radius. Set to 0 to have no hole
r_hole=2;

//side idents to take out the swatch from the box. set to 0 to have no idents
r_ident=2.5;
 

/* [Hidden] */
//-------------------------------------------------------------------------------

/*
Configurable Filament Swatch 
Version F, January 2019
Written by MC Geisler (mcgenki at gmail dot com)

This is a swatch with configurable text.

Have fun!

License: Attribution 4.0 International (CC BY 4.0)

You are free to:
    Share - copy and redistribute the material in any medium or format
    Adapt - remix, transform, and build upon the material
    for any purpose, even commercially.
*/


$fn=48;

font_recessed=1.5;

module rounded_square(x,y,z,r)
{
    delta=r;
    
    hull()
    {
        translate([r,r,0]) cylinder(h=z,r=r);
        translate([-r+x,r,0]) cylinder(h=z,r=r);
        translate([r,-r+y,0]) cylinder(h=z,r=r);
        translate([-r+x,-r+y,0])cylinder(h=z,r=r);
    }
}

module rounded_biteoff_square(x,y,z,r)
{  
    delta=r;
    
    difference()
    {
        translate([-delta,0,0])
            cube([x+2*r,y,z]);
        
        translate([-2*r,0,0])
        hull()
        {
            translate([r,r,-.1]) cylinder(h=z+1,r=r);
            translate([r,-r+y,-.1]) cylinder(h=z+1,r=r);
        }
        
        translate([2*r,0,0])
        hull()
        {
            translate([-r+x,-r+y,-.1])cylinder(h=z+1,r=r);
            translate([-r+x,r,-.1]) cylinder(h=z+1,r=r);
        }
    }
}


module rounded_square_test_pattern(x,y,z,r)
{
    delta=r;
    r3=z/2;
    r2=r;

    difference()
    {
        hull()
        {
            //bottom left cylindrical/round
            translate([r,r,0]) cylinder(h=z,r=r);
            
            //bottom right test flattened/round
//            translate([-r2+x-delta,r2,0]) cylinder(h=z,r=r2);
//            translate([-r2+x,r2+delta,0]) cylinder(h=z,r=r2);
//            translate([-r2+x,r2,z/2]) 
//                scale([r2,r2,z/2])
//                    sphere(r=1);
            translate([-r+x,r,0]) cylinder(h=z,r=r);
            
            //upper left ball/rounded
             translate([r,-r+y,0]) cylinder(h=z,r=r);
            //translate([r3,-r3+y,r3]) scale([r3,r3,z/2]) sphere(r=1);
            
            //upper right corner
            r_corner=round;
            translate([-r_corner+x,-r_corner+y,0]) cylinder(h=z,r=r_corner);
        }
        
      
        //text on top
        fontdepth=.6;
        translate([x-round,y-fontdepth,z-.4]) 
        //translate([ w-2*wall,h-wall-textsize*(1+linesep),height ])
            rotate([-90,0,0])
                linear_extrude(height=fontdepth*2,convexity = 10)
                    text(texttop,size=z-.8,font="Open Sans:style=Bold",halign="right",valign="bottom");    
    }
}




module textlines(height)
{
    translate([ w-wall,h-wall-textsize,height ])
        linear_extrude(height=th,convexity = 10)
            text(textstring1,size=textsize,font=fontname,halign="right",valign="baseline");  
    
    translate([ wall,h-wall-textsize*(1+linesep),height ])
        linear_extrude(height=th,convexity = 10)
                text(textstring2,size=textsize,font=fontname,halign="left",valign="baseline");  
    
    translate([ w-wall,h-wall-textsize*(1+linesep),height ])
        linear_extrude(height=th,convexity = 10)
                text(textstring3,size=textsize,font=fontname,halign="right",valign="baseline");      
}


module swatch(text_type, test_patterns)
{            
    //wall thickness around step area 
    stepareadistance=(test_patterns=="enabled"?1:1);
    //height of the text field -> needs more space if raised text
    font_h=( (text_type=="raised"?linesep:1) + linesep)*textsize;
    //step area height
    steparea_h=h-(stepareadistance+1)*wall-font_h-wall;
    //if raised text, what is the area to recess so the raised text goes on top
    fontarea_h=font_h+wall;
        
    difference()
    {
        if(test_patterns=="enabled")
            rounded_square_test_pattern(w,h,th,round);
        else
            rounded_square(w,h,th,round);

        //hole to hang
        translate([wall+r_hole,h-wall-r_hole,th/2])
            cylinder(r=r_hole, h=th*2, center=true);
                            
        for (i = [0:len(thickness_steps)-1])
        {
            steparea_w=(w-(2*stepareadistance)*wall)/len(thickness_steps)*(i+1);
            
            translate([ wall*stepareadistance,wall*stepareadistance,(thickness_steps[i]<0?-.1:thickness_steps[i]) ])
            {
                rounded_square(steparea_w,steparea_h,th*2,round-wall);
            }
        }
        
        //handles
        handle_groove_out=r_ident/2;
        translate([-handle_groove_out,h-wall-r_ident-1,th/2])
            cylinder(r=r_ident, h=th*2, center=true);
        translate([w+handle_groove_out,h-wall-r_ident-1,th/2])
            cylinder(r=r_ident, h=th*2, center=true);
        
        if (text_type=="embossed")
        {
            //text
            textlines(th-font_recessed);
        }
        else
        {//xxx     if(text_type!="embossed")
            translate([ wall/2,h-wall/2-fontarea_h,th-font_recessed ])
                rounded_square(w-wall,fontarea_h,th,round-wall/2);
        }
    }

    //thickness numbers & bridges
    textheight=textheight_on_steps;//th-thickness_steps[len(thickness_steps)-1];
    textheight_bridge=textheight_on_steps;
    
    one_steparea_w=(w-2*stepareadistance*wall)/len(thickness_steps);
    for (i = [0:len(thickness_steps)-1])
    {
        steparea_w=one_steparea_w*(i+1);
        translate([ wall*stepareadistance+ steparea_w,0,0 ])
        {    
            {
                translate([ -one_steparea_w/10,wall*2,(thickness_steps[i]<0?th-textheight_bridge:thickness_steps[i])])
                    linear_extrude(height=(thickness_steps[i]<0?textheight_bridge:textheight),convexity = 10)
                        text(str(abs(thickness_steps[i])),size=textsize,font=fontname,halign="right",valign="baseline");
            }
            if (thickness_steps[i]<0) // bridge
            {
                gap=round/3;
                translate([ -one_steparea_w+gap,wall*stepareadistance,th-(-thickness_steps[i])-textheight_bridge ])
                    cube([one_steparea_w-gap, steparea_h, -thickness_steps[i]]);
                    //rounded_biteoff_square(one_steparea_w-gap, steparea_h, -thickness_steps[i],round-wall);
            }
        }  
    }
    
    if(text_type!="embossed")
    {    
        textlines(0);
    }

    

}
 

Debug="false";
if (Debug!="true")
{
    swatch(text_type, test_patterns);
}
else
{
    swatch("raised", "disabled");
    translate([w+3,0,0]) swatch("raised", "enabled");
    translate([0,h+3,0]) swatch("embossed", "disabled");
    translate([w+3,h+3,0]) swatch("embossed", "enabled");
}
